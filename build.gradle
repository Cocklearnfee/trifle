// project description
description = "trifle root project"

// default project tasks
defaultTasks "clean", "build"

// extensional properties
ext {
    publishSnapshots = Boolean.getBoolean("publishSnapshots")
    publishReleases = Boolean.getBoolean("publishReleases")
    sonatypeUsername = System.getProperty("sonatypeUsername")
    sonatypePassword = System.getProperty("sonatypePassword")
}

// build script for gradle
buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()

        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

// all projects config
allprojects {
    group 'trifle'
    version '1.0.0'

    apply plugin: "idea"

    idea {
        module {
            downloadSources = true
            downloadJavadoc = true
            jdkName = JavaVersion.VERSION_1_8
        }
    }
}

// sub projects config
subprojects {
    //子项目使用的插件
    //java 插件使用之后能够在 dependencies 中使用 compile/testCompile 等方法。
    //避免在父模块中使用 java 插件，因为父项目没有必要生成 parent_main 和 parent_test 模块
    apply plugin: 'java'

    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'

    apply from: rootProject.file("gradle/tasks.gradle")

    //apply plugin: 'java' 之后才有属性 - 代码源兼容版本
    sourceCompatibility = JavaVersion.VERSION_1_8
    //apply plugin: 'java' 之后才有属性 - 目标兼容版本
    targetCompatibility = JavaVersion.VERSION_1_8

    // repositories for sub projects
    repositories {
        // local maven repository
        mavenLocal()
        jcenter()

        // maven central repositories
        mavenCentral()
    }

    test {
        enabled = !Boolean.getBoolean("skipTests")
        useJUnit()
        scanForTestClasses false
        include '**/*Tests.*'
        exclude '**/*Abstract*.*'
        classpath = it.classpath + files("${projectDir}/src/main/resources") +
                files("${projectDir}/src/test/resources")
    }

    // 配置项目依赖配置
    configurations {
        // 添加 provided 配置，用来在 dependencies 中区别声明依赖类型
        providedCompile

        // 将给定的 provided 配置添加到 compile 中
        compile.extendsFrom(providedCompile)
    }

    // 配置 jar 任务
    jar {
        manifest {
            attributes("Implementation-Title": project.name,
                    "Implementation-Vendor": project.group,
                    "Created-By": project.group,
                    "Implementation-Version": project.version)
        }
    }

    dependencies {
        compile fileTree("libs")

        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        providedCompile "javax.servlet:javax.servlet-api:${javaxServletVersion}"

        runtime "org.slf4j:jul-to-slf4j:${slf4jVersion}"
        runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
        runtime "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
        runtime "ch.qos.logback:logback-classic:${logbackVersion}"


        testCompile "junit:junit:${junitVersion}"
        testCompile "org.springframework:spring-test:${springVersion}"

    }
}
